#compdef freerange
 
local curcontext="$curcontext" state line ret=1

_arguments -C \
  '(- 1 *)'{-h,--help}'[display help information]' \
  '(- 1)--version[display version information]' \
  '1: :->cmds' \
  '*: :->args' && ret=0
 
case $state in
  cmds)
    if (( ! $+_freerange_cmds )) ; then
      typeset -gH _freerange_cmds
      _freerange_cmds=( $(freerange help | grep '^  [a-z]' | cut -d " " -f 4) )
    fi
    compadd $_freerange_cmds && ret=0
  ;;
  args)
    case $line[1] in
      (pair)
        if (( ! $+_freerange_members )) ; then
          typeset -gH _freerange_members
          _freerange_members=( $(freerange members | grep '^[A-Za-z]' | cut -d " " -f 1) )
        fi
        compadd $_freerange_members && ret=0
      ;;
    esac
  ;;
esac
 
return ret